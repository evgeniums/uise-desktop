@ECHO OFF

IF "%MINGW_PATH%"=="" SET MINGW_PATH=C:\msys64\mingw64\bin
IF "%QT_HOME_MINGW%"=="" SET QT_HOME_MINGW=C:\Qt\6.0.1\mingw81_64
IF "%DEPS_UNIVERSAL_ROOT%"=="" SET DEPS_UNIVERSAL_ROOT=C:\projects\dracosha\deps
IF "%BUILD_WORKERS%"=="" SET BUILD_WORKERS=4

IF "%UISE_COMPILER%"=="" SET UISE_COMPILER=gcc
IF "%UISE_BUILD%"=="" SET UISE_BUILD=release

IF "%UISE_COMPILER%" == "gcc" (
    SET BOOST_ROOT=%DEPS_UNIVERSAL_ROOT%\root-mingw-x86_64
    SET QT_HOME=%QT_HOME_MINGW%
    SET EXTRA_PATH=%MINGW_PATH%
    SET TOOLCHAIN=mingw64
) ELSE (
    SET BOOST_ROOT=%DEPS_UNIVERSAL_ROOT%\root-msvc-14.2-x86_64
    SET QT_HOME=%QT_HOME_MSVC%
    SET TOOLCHAIN=msvc
)
SET PATH=%QT_HOME%/bin;%BOOST_ROOT%\lib;%EXTRA_PATH%;%PATH%

IF "%UISE_BUILD%" == "release" (
SET BUILD_TYPE=Release
)
IF "%UISE_BUILD%" == "debug" (
SET BUILD_TYPE=Debug
)
IF "%UISE_BUILD%" == "minsize_release" (
SET BUILD_TYPE=MinSizeRel
)

SET BUILD_DIR=builds\build-%TOOLCHAIN%-%UISE_BUILD%
SET SRC_DIR=%~dp0..

echo %BUILD_DIR%
echo %SRC_DIR%

IF EXIST %BUILD_DIR% rmdir /Q /S %BUILD_DIR%
mkdir %BUILD_DIR%
SET CURRENT_DIR=%CD%
cd %BUILD_DIR%

IF "%UISE_COMPILER%" == "gcc" (
cmake -G "MinGW Makefiles" -DUISE_TEST_JUNIT=ON -DCMAKE_BUILD_TYPE=%BUILD_TYPE% %SRC_DIR% 
call "mingw32-make" -j%BUILD_WORKERS%
)

cd %CURRENT_DIR%